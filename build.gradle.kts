plugins {
	java
	application
	kotlin("jvm") version "1.9.24"
	id("org.javamodularity.moduleplugin") version "1.8.15"
	id("org.openjfx.javafxplugin") version "0.0.13"
	id("org.beryx.jlink") version "2.25.0"
}

group = "com.catand"
version = "1.0-SNAPSHOT"

repositories {
	mavenCentral()
}

val junitVersion = "5.12.1"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

tasks.withType<JavaCompile> {
	options.encoding = "UTF-8"
}

application {
	mainModule.set("com.catand.snake")
	mainClass.set("com.catand.snake.SnakeApp")
}

javafx {
	version = "21.0.6"
	modules = listOf("javafx.controls", "javafx.fxml", "javafx.web", "javafx.swing", "javafx.media")
}

dependencies {
	compileOnly ("org.projectlombok:lombok:1.18.42")
	annotationProcessor ("org.projectlombok:lombok:1.18.42")
	implementation("org.controlsfx:controlsfx:11.2.2")
	implementation("net.synedra:validatorfx:0.6.1") {
		exclude(group = "org.openjfx")
	}
	implementation("org.kordamp.ikonli:ikonli-javafx:12.4.0")
	implementation("eu.hansolo:tilesfx:21.0.9") {
		exclude(group = "org.openjfx")
	}
	implementation("com.github.almasb:fxgl:21.1") {
		exclude(group = "org.openjfx")
		exclude(group = "org.jetbrains.kotlin")
	}
	testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
	implementation("org.jetbrains.kotlin:kotlin-stdlib:${properties["kotlin.version"] ?: "1.9.24"}")
}

tasks.withType<Test> {
	useJUnitPlatform()
}

jlink {
	imageZip.set(layout.buildDirectory.file("/distributions/app-${javafx.platform.classifier}.zip"))
	options.set(listOf("--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"))
	launcher {
		name = "app"
	}
}
